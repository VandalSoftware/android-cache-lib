/*
 * Copyright (C) 2013 Vandal LLC
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.3"

ext.isReleaseVersion = has("release")

if (isReleaseVersion) {
    ext.sonatypeRepo = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    version += "-SNAPSHOT"
    ext.sonatypeRepo = "https://oss.sonatype.org/content/repositories/snapshots/"
}

group = "com.vandalsoftware.android"

ext.sonatypeUsername = has("sonatypeUsername") ? sonatypeUsername : ""
ext.sonatypePassword = has("sonatypePassword") ? sonatypePassword : ""

android {
    compileSdkVersion 17
    buildToolsVersion '17.0.0'
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

repositories {
    mavenCentral()
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task publishLocal(type: Upload) {
    configuration = configurations.archives
    repositories.mavenDeployer {
        repository(url: "file://$buildDir/repo")
    }
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: project.ext.sonatypeRepo) {
            authentication(userName: project.ext.sonatypeUsername,
                    password: project.ext.sonatypePassword)
        }

        pom.project {
            name 'Android Cache Library'
            packaging 'aar'
            description 'Cache library for Android applications'
            url 'https://github.com/VandalSoftware/android-cache-lib'

            scm {
                url 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
                connection 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
                developerConnection 'scm:git@github.com:VandalSoftware/android-cache-lib.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'jle'
                    name 'Jonathan Le'
                    email 'jonathanle@gmail.com'
                }
            }
        }
    }
}
